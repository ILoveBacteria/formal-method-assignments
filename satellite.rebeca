reactiveclass Coordinator(5) {
	knownrebecs {
        Satellite s1;
        Satellite s2;
        Satellite s3;
    }
    
    statevars {
        byte slot;
        byte ack_count;
    }
    
    Coordinator() {
    	slot = 0;
        ack_count = 0;
        initial();
    	slotManager();
    }

    void initial() {
        s1.receive(42, 1, 2);
        s1.receive(42, 1, 3);
        s2.receive(5, 2, 0);
        s3.receive(50, 3, 2);
        s2.receive(5, 2, 3);
    }

    msgsrv ack() {
        ack_count -= 1;
        if (ack_count == 0) {
            // all satellites acknowledged
            slotManager();
        }
    }
    
    msgsrv slotManager() {
    	// reset satellite
    	s1.slotChanged(-1);
    	s2.slotChanged(-1);
    	s3.slotChanged(-1);
        ack_count = 3;
    	// increase slot and publish new slot
    	if (slot == 0) {
    		s1.slotChanged(0);
            ack_count += 1;
    	} else if (slot == 1) {
    		s2.slotChanged(0);
            ack_count += 1;
    	} else if (slot == 2) {
    		s3.slotChanged(0);
            ack_count += 1;
    	} else if (slot == 4) {
    		s1.slotChanged(2);
    		s2.slotChanged(1);
            ack_count += 2;
    	} else if (slot == 5) {
    		s3.slotChanged(2);
    		s2.slotChanged(3);
            ack_count += 2;
    	} else if (slot == 6) {
    		s1.slotChanged(3);
    		s3.slotChanged(1);
            ack_count += 2;
    	}
    	slot = (slot + 1) % 8;
    }
}

reactiveclass Satellite(5) {
	knownrebecs {
        Ground gr;
        Satellite s1;
        Satellite s2;
        Satellite s3;
    }
    
    statevars {
        byte receiver;
        byte energy;
        byte id;
        boolean safe_mode;
    }
    
    Satellite(byte my_id) {
    	receiver = -1;
    	energy = 100;
    	id = my_id;
        safe_mode = false;
    }
    
    msgsrv slotChanged(byte r) {
    	assertion(r <= 3 && r >= -1);
    	// update energy
    	if (energy + 8 >= 100) {
    		energy = 100;
    	} else {
    		energy += 8;
    	}
        if (energy > 20) {
            safe_mode = false;
        }
    	receiver = r;
        ((Coordinator)sender).ack();
    }

    msgsrv receive(byte msg, byte src_id, byte dest_id) {
        if (energy < 6) {
            return;
        }
        // receive message
        energy -= 5;
        self.send(msg, src_id, dest_id);
        // check energy level
        if (energy < 10) {
            safe_mode = true;
        }
    }

    msgsrv send(byte msg, byte src_id, byte dest_id) {
        if (receiver == -1) {
        	send(msg, src_id, dest_id);
        	return;
        }
        
        // send message
        if (energy >= 20) {
            if (receiver == id) {
            	return;
            } else if (receiver == 0) {
            	gr.receive(msg, id, dest_id);
                energy -= 15;
            } else if (receiver == 1) {
            	s1.receive(msg, id, dest_id);
                energy -= 10;
            } else if (receiver == 2) {
            	s2.receive(msg, id, dest_id);
                energy -= 10;
            } else if (receiver == 3) {
            	s3.receive(msg, id, dest_id);
                energy -= 10;
            }
        }

        if (energy < 10) {
            safe_mode = true;
        }
    }
}

reactiveclass Ground(5) {
    statevars {
        byte[3] count_received_messages;
        byte message;
    }

    Ground() {
        count_received_messages[0] = 0;
        count_received_messages[1] = 0;
        count_received_messages[2] = 0;
    }

    msgsrv receive(byte msg, byte src_id, byte dest_id) {
        message = msg;
        count_received_messages[src_id-1] += 1;
    }
}

main {
    Coordinator coordinator(satellite1, satellite2, satellite3):();
    Ground ground():();
    Satellite satellite1(ground, satellite1, satellite2, satellite3):(1);
    Satellite satellite2(ground, satellite1, satellite2, satellite3):(2);
    Satellite satellite3(ground, satellite1, satellite2, satellite3):(3);
}
